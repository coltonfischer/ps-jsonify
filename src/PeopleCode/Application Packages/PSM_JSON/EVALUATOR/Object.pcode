import PSM_JSON:TYPE:*;
import PSM_JSON:EVALUATOR:Class;
import PSM_JSON:Node;

class Object
   method Object(&poValue As object, &pjnJson As PSM_JSON:Node);
   method CreateNode();
   method CreateNodeForType(&psType As string);
private
   instance object &moValue;
   instance PSM_JSON:Node &mjnJson;
   method IsAppClass(&psType As string) Returns boolean;
   method IsJavaClass(&psType As string) Returns boolean; /* @todo */
end-class;


method Object
   /+ &poValue as Object, +/
   /+ &pjnJson as PSM_JSON:Node +/
   &moValue = &poValue;
   &mjnJson = &pjnJson;
end-method;


method CreateNode
   
   %This.CreateNodeForType(String(&moValue));
   
end-method;


method CreateNodeForType
   /+ &psType as String +/
   
   If %This.IsAppClass(&psType) Then
      
      (create PSM_JSON:EVALUATOR:Class(&moValue, &mjnJson)).CreateNode();
      
      Return;
      
   End-If;
   
   Evaluate Upper(&psType)
   When "ARRAY"
      (create PSM_JSON:TYPE:Array(&moValue, &mjnJson)).Process();
      Return;
   When "RECORD"
      &mjnJson.SetRecord(&moValue);
      Return;
   When "ROWSET"
      &mjnJson.SetRowset(&moValue);
      Return;
   When "ROW"
      &mjnJson.SetRow(&moValue);
      Return;
   When "HASH"
      &mjnJson.SetHash(&moValue);
      Return;
   When "NULL"
      &mjnJson.SetNull();
      Return;
   When "EXCEPTION"
      &mjnJson.SetException(&moValue);
      Return;
   When "REQUEST"
      &mjnJson.SetRequest(&moValue);
      Return;
   When "RESPONSE"
      &mjnJson.SetResponse(&moValue);
      Return;
   When "COOKIE"
      &mjnJson.SetCookie(&moValue);
      Return;
   When "JSONVALUE"
      &mjnJson.SetJsonValue(&moValue);
      Return;
   When "JSONOBJECT"
      &mjnJson.SetJsonObject(&moValue);
      Return;
   When "JSONARRAY"
      &mjnJson.SetJsonArray(&moValue);
      Return;
   When "JSONNODE"
      &mjnJson.SetJsonNode(&moValue);
      Return;
   When-Other
      &mjnJson.SetString(&psType);
   End-Evaluate;
   
end-method;


method IsAppClass
   /+ &psType as String +/
   /+ Returns Boolean +/
   
   Return (Find(":", &psType) > 0);
   
end-method;


method IsJavaClass
   /+ &psType as String +/
   /+ Returns Boolean +/
   
   Return (Find(".", &psType) > 0);
   
end-method;