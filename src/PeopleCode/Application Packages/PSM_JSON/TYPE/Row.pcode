import PSM_JSON:Object;

class Row
   method Row(&poValue As Row, &pjnJson As JsonNode);
   method Process();
private
   instance Row &moValue;
   instance PSM_JSON:Object &mjoJson;
end-class;

method Row
   /+ &poValue as Row, +/
   /+ &pjnJson as JsonNode +/
   
   &moValue = &poValue;
   
   If Not &pjnJson.IsJsonObject() Then
      &pjnJson.SetJsonObject(CreateJsonObject());
   End-If;
   
   &mjoJson = create PSM_JSON:Object();
   &mjoJson.FromJsonObject(&pjnJson.GetJsonObject());
   
end-method;


method Process
   
   Local integer &i;
   
   For &i = 1 To &moValue.RecordCount
      
      Local Record &rRecord = &moValue.GetRecord(&i);
      &mjoJson.AddRecord(&rRecord.Name, &rRecord);
      
   End-For;
   
   For &i = 1 To &moValue.ChildCount
      
      Local Rowset &rsObject = &moValue.GetRowset(&i);
      &mjoJson.AddRowset(&rsObject.DBRecordName, &rsObject);
      
   End-For;
   
end-method;